(ns webstack.client-manifest
  "Defines the client-manifest component, which reads in `manifest.edn` file
  generated by ShadowCLJS."
  (:require
   [mount.core :refer [defstate]]
   [clojure.edn :as edn]))

(defstate client-manifest
  "The client application config, read in from the manifest file output by
  ShadowCLJS. This is essential to get the file name, which includes the
  hash to prevent caching issues when it changes."
  :start (edn/read-string (slurp "resources/public/js/manifest.edn")))

(defn get-client-module-filename
  "Retrieves the name of client file output by ShadowCLJS for the given module.
  Looks up the `:main` module by default. `manifest` is the parsed data from
  ShadowCLJS's `manifest.edn` file."
  ([manifest module-id]
   (->> manifest
        (filter #(= module-id (:module-id %)))
        first
        :output-name))
  ([manifest]
   (get-client-module-filename manifest :main)))
